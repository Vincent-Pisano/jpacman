plugins {
    id 'application'
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'pmd'
    id 'com.github.spotbugs' version '1.7.1'
    id 'org.sonarqube'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('.env')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withInputStream { localProperties.load(it) }
}

sourceSets {
    defaultTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/default-test/java')
        }
        resources.srcDir file('src/default-test/resources')
    }
}

configurations {
    defaultTestCompile.extendsFrom testCompile
    defaultTestImplementation.extendsFrom testImplementation
    defaultTestRuntime.extendsFrom testRuntime
}

repositories {
    mavenCentral()
}

dependencies {
    compile "com.google.guava:guava:$guavaVersion"


    compileOnly "com.github.spotbugs:spotbugs-annotations:$spotbugsAnnotationsVersion"

    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testCompile "org.assertj:assertj-core:$assertjVersion"
}

task defaultTest(type: Test) {
    testClassesDirs = sourceSets.defaultTest.output.classesDirs
    classpath = sourceSets.defaultTest.runtimeClasspath
}
test.dependsOn defaultTest

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

defaultTest {
    useJUnitPlatform()
}

// Console details for tests (names + failures)
tasks.withType(Test) {
    testLogging {
        events "failed", "skipped"

        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
        showStandardStreams false
    }
}

jacoco {
    toolVersion = jacocoVersion
}

jacocoTestReport {
    dependsOn test, defaultTest

    executionData fileTree(buildDir).include(
        "jacoco/test.exec",
        "jacoco/defaultTest.exec"
    )

    reports {
        csv.enabled true
        xml.enabled true
    }
}

import java.math.BigDecimal
import java.math.RoundingMode

tasks.register("printcov") {
    group = "verification"
    description = "Prints overall/line/condition coverage from the JaCoCo XML report (sourcefile-level, Sonar-like)."
    dependsOn tasks.jacocoTestReport

    doLast {
        def xmlFile = file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
        if (!xmlFile.exists()) {
            logger.lifecycle("JaCoCo XML report not found at: ${xmlFile}")
            return
        }

        def xmlText = xmlFile.getText("UTF-8")
        xmlText = xmlText.replaceFirst("(?s)<!DOCTYPE.*?>", "")
        def report = new XmlSlurper(false, false).parseText(xmlText)

        long lineMissed = 0, lineCovered = 0
        long branchMissed = 0, branchCovered = 0

        report.'package'.each { pkg ->
            def pkgName = pkg.@name.text() // e.g. nl/tudelft/jpacman
            pkg.'sourcefile'.each { sf ->
                def fileName = sf.@name.text() // e.g. Launcher.java
                def relPath = pkgName ? "${pkgName}/${fileName}" : fileName

                def src = file("src/main/java/${relPath}")
                if (!src.exists()) return

                def lc = sf.'counter'.find { it.@type.text() == "LINE" }
                if (lc) {
                    lineMissed += lc.@missed.toLong()
                    lineCovered += lc.@covered.toLong()
                }

                def bc = sf.'counter'.find { it.@type.text() == "BRANCH" }
                if (bc) {
                    branchMissed += bc.@missed.toLong()
                    branchCovered += bc.@covered.toLong()
                }
            }
        }

        long linesToCover = lineMissed + lineCovered
        long conditionsToCover = branchMissed + branchCovered

        long uncoveredLines = lineMissed
        long uncoveredConditions = branchMissed

        long totalToCover = linesToCover + conditionsToCover
        long totalCovered = lineCovered + branchCovered

        def pct = { long covered, long total ->
            if (total == 0L) return new BigDecimal("100.0")
            return (new BigDecimal(covered) * new BigDecimal("100.0"))
                .divide(new BigDecimal(total), 4, RoundingMode.HALF_UP)
        }

        def fmt = { BigDecimal v -> v.setScale(1, RoundingMode.HALF_UP).toPlainString() + "%" }

        logger.lifecycle("")
        logger.lifecycle("Overall")
        logger.lifecycle("Coverage             ${fmt(pct(totalCovered, totalToCover))}")
        logger.lifecycle("Lines to Cover       ${linesToCover}")
        logger.lifecycle("Uncovered Lines      ${uncoveredLines}")
        logger.lifecycle("Line Coverage        ${fmt(pct(lineCovered, linesToCover))}")
        logger.lifecycle("Conditions to Cover  ${conditionsToCover}")
        logger.lifecycle("Uncovered Conditions ${uncoveredConditions}")
        logger.lifecycle("Condition Coverage   ${fmt(pct(branchCovered, conditionsToCover))}")
        logger.lifecycle("")
    }
}


tasks.named("sonarqube") {
    dependsOn jacocoTestReport
}

checkstyle {
    configFile file("checkstyle.xml")
    ignoreFailures = true
}

pmd {
    ruleSetFiles "pmd-rules.xml"
    ruleSets = []
}

sonarqube {
    properties {
        property "sonar.coverage.jacoco.xmlReportPaths",
                "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"

        property "sonar.junit.reportPaths",
                "${buildDir}/test-results/test,${buildDir}/test-results/defaultTest"

        property "sonar.sources", "src/main/java"
        property "sonar.tests", "src/test/java,src/default-test/java"

        property "sonar.java.binaries", "${buildDir}/classes/java/main"
        property "sonar.java.test.binaries",
                "${buildDir}/classes/java/test,${buildDir}/classes/java/defaultTest"

        property "sonar.java.libraries",
        configurations.runtimeClasspath.files.collect { it.absolutePath }.join(',')

        property "sonar.java.test.libraries",
                (configurations.runtimeClasspath.files + configurations.testRuntimeClasspath.files)
                        .collect { it.absolutePath }
                        .unique()
                        .join(',')


        if (localProperties.getProperty('sonar.projectKey')) {
            property "sonar.projectKey", localProperties.getProperty('sonar.projectKey')
        }
        if (localProperties.getProperty('sonar.host.url')) {
            property "sonar.host.url", localProperties.getProperty('sonar.host.url')
        }
        if (localProperties.getProperty('sonar.login')) {
            property "sonar.login", localProperties.getProperty('sonar.login')
        }
    }
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

task staticAnalysis(type: GradleBuild) {
    tasks = [
        'clean',
        'checkstyleMain',
        'checkstyleTest',
        'checkstyleDefaultTest',
        'pmdMain',
        'pmdTest',
        'pmdDefaultTest',
        'spotbugsMain',
        'spotbugsTest',
        'spotbugsDefaultTest',
    ]
}

mainClassName = 'nl.tudelft.jpacman.Launcher'
